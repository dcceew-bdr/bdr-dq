# This rule checks if the date has correct format (YYYY-MM-DD).
# Only observations with comment "NSL name match Observation" are checked.

PREFIX sosa:   <http://www.w3.org/ns/sosa/>
PREFIX time:   <http://www.w3.org/2006/time#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dqaf:   <http://example.com/def/dqaf/>
PREFIX schema: <http://schema.org/>
PREFIX xsd:    <http://www.w3.org/2001/XMLSchema#>

INSERT {
  GRAPH dqaf:fullResults {
    ?observation dqaf:hasResult [
      sosa:observedProperty <http://example.com/assess/date_format_validation/> ;
      schema:value ?dateFormatValidation
    ]
  }
}
WHERE {
  # Get time from observation
  ?observation sosa:phenomenonTime ?timeNode .
  ?timeNode time:inXSDDateTimeStamp ?dateLiteral .

  # Filter by rdfs:comment
  ?observation rdfs:comment ?comment .
  FILTER(CONTAINS(STR(?comment), "NSL name match Observation"))

  # Take only date part (first 10 characters)
  BIND(SUBSTR(STR(?dateLiteral), 1, 10) AS ?dateStr)

  # Check format with regex (must be YYYY-MM-DD)
  BIND(
    IF(REGEX(?dateStr, "^[0-9]{4}-[0-9]{2}-[0-9]{2}$"), "valid", "invalid")
    AS ?dateFormatValidation
  )
}
