# This rule checks how many decimal digits are in lon/lat values.
# It returns High, Medium, or Low based on precision.

PREFIX tern:   <https://w3id.org/tern/ontologies/tern/>
PREFIX sosa:   <http://www.w3.org/ns/sosa/>
PREFIX geo:    <http://www.opengis.net/ont/geosparql#>
PREFIX dqaf:   <http://example.com/def/dqaf/>
PREFIX schema: <http://schema.org/>

INSERT {
  GRAPH dqaf:fullResults {
    ?observation dqaf:hasResult [
      sosa:observedProperty <http://example.com/assess/coordinate_precision/> ;
      schema:value ?coordinate_precision
    ]
  }
}
WHERE {
  ?observation a tern:Observation ;
               sosa:hasFeatureOfInterest ?sample .
  ?sample a tern:Sample ;
          sosa:isResultOf ?procedure .

  OPTIONAL {
    ?procedure geo:hasGeometry ?geometry .
    OPTIONAL {
      ?geometry geo:asWKT ?wkt .

      # Get "lon lat" part from WKT string
      BIND(REPLACE(STR(?wkt), "POINT\\(\\s*(.*)\\s*\\)", "$1") AS ?coords)
      BIND(STRBEFORE(?coords, " ") AS ?lonStr)
      BIND(STRAFTER(?coords, " ") AS ?latStr)

      # Get text after decimal (if exists)
      BIND(IF(CONTAINS(?lonStr, "."), STRAFTER(?lonStr, "."), "") AS ?lonDec)
      BIND(IF(CONTAINS(?latStr, "."), STRAFTER(?latStr, "."), "") AS ?latDec)

      # Count decimal digits
      BIND(STRLEN(?lonDec) AS ?lonPrecision)
      BIND(STRLEN(?latDec) AS ?latPrecision)

      # Decide level: High (>4), Low (<2), Medium (else)
      BIND(
        IF(
          (?lonPrecision > 4 && ?latPrecision > 4),
          "High",
          IF(
            (?lonPrecision < 2 && ?latPrecision < 2),
            "Low",
            "Medium"
          )
        )
        AS ?coordinate_precision
      )
    }
  }
}
