# assess_coordinate_completeness

#-------------------------------------------------------
PREFIX tern: <https://w3id.org/tern/ontologies/tern/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?observation ?wkt 
       (IF(BOUND(?wkt) && STRLEN(STR(?wkt)) > 0, "non_empty", "empty") AS ?completeness)
       (IF(BOUND(?wkt) && STRLEN(STR(?wkt)) > 0,
           REPLACE(?wktNoCRS, "^POINT\\s*\\(([^ ]+)\\s+([^ ]+)\\)$", "$1, $2"),
           "0, 0") AS ?location)
WHERE {
  ?observation a tern:Observation .
  ?observation sosa:hasFeatureOfInterest ?sample .
  ?sample a tern:Sample .
  ?sample sosa:isResultOf ?procedure .
  ?procedure geo:hasGeometry ?geometry .
  OPTIONAL { ?geometry geo:asWKT ?wkt . }
  
  BIND(
    IF(STRSTARTS(STR(?wkt), "<"),
       SUBSTR(STR(?wkt), STRLEN(STRBEFORE(STR(?wkt), ">")) + 3),
       STR(?wkt)
    ) AS ?wktNoCRS
  )
}