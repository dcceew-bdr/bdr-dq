assess_coordinate_unusual

-------------------------------------------------------
PREFIX tern: <https://w3id.org/tern/ontologies/tern/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?observation ?wkt ?wktNoCRS
       (IF((REGEX(?lonDec, "([0-9])\\1\\1") || REGEX(?latDec, "([0-9])\\1\\1")),
           "unusual", "usual") AS ?coordinateUnusual)
       ?lonStr ?latStr ?lonDec ?latDec
WHERE {
  ?observation a tern:Observation .
  ?observation sosa:hasFeatureOfInterest ?sample .
  ?sample a tern:Sample .
  ?sample sosa:isResultOf ?procedure .
  ?procedure geo:hasGeometry ?geometry .
  ?geometry geo:asWKT ?wkt .

  BIND(
    IF(STRSTARTS(STR(?wkt), "<"),
       SUBSTR(STR(?wkt), STRLEN(STRBEFORE(STR(?wkt), ">")) + 3),
       STR(?wkt)
    ) AS ?wktNoCRS
  )

  BIND(REPLACE(?wktNoCRS,
       "^POINT\\s*\\(([-+]?\\d+\\.\\d+)[\\s]+([-+]?\\d+\\.\\d+)\\)$",
       "$1") AS ?lonStr)
  BIND(REPLACE(?wktNoCRS,
       "^POINT\\s*\\(([-+]?\\d+\\.\\d+)[\\s]+([-+]?\\d+\\.\\d+)\\)$",
       "$2") AS ?latStr)

  BIND(STRAFTER(?lonStr, ".") AS ?lonDec)
  BIND(STRAFTER(?latStr, ".") AS ?latDec)
}
-------------------------------------------------------
